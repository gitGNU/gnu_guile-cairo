#!/bin/bash
# -*- scheme -*-
exec guile --debug -e main -s "$0" "$@"
!#

(use-modules (gnome gw support gtk-doc)
             (texinfo serialize))

(define (docbook->texi-stubs . files)
  (for-each
   (lambda (file)
     (let* ((sdocbook (docbook->sdocbook file))
            (basename (basename file))
            (docs `(*fragment*
                    (node (% (name ,@(gtk-doc-sdocbook-title sdocbook))))
                    (chapter ,@(gtk-doc-sdocbook-title sdocbook)
                             ": " ,@(gtk-doc-sdocbook-subtitle sdocbook))
                    (section "Overview")
                    ,@(cdr (gtk-doc-sdocbook->description-fragment sdocbook))
                    (section "Usage")
                    (include ,(string-append "defuns-" basename ".texi")))))
       (with-output-to-file (string-append "section-" basename ".texi")
         (lambda ()
           (display (stexi->texi docs))))))
   files))

(define (main args)
  (apply docbook->texi-stubs (cdr args)))
